if not build_userhacks
    subdir_done()
endif

sources = [
    'Logitech-MX-Master-3B-middle-button.bpf.c',
    'mouse_invert_y.bpf.c',
    'trace_hid_events.bpf.c',
]

foreach bpf : sources
    filter_matches = not have_filters
    if have_filters
        foreach filter : filters
            if bpf.contains(filter)
                filter_matches = true
                break
            endif
        endforeach
    endif
    if filter_matches
        bpf_sources += files(bpf)
        # This is a dummy target so meson thinks there's something here that
        # we can install. The actual files are generated by the cargo command
        # but adding a custom command here means we can install the files.
        bpf_o_files_userhacks += custom_target('fake-@0@.o'.format(bpf),
            input: bpf,
            depend_files: bpf,
            output: ['@BASENAME@.o'],
            command: ['true'],
            install: true,
            install_dir: bpfdir,
        )
    endif
endforeach
