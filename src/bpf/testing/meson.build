if not build_testing
    subdir_done()
endif

sources = [
    'HP_Elite_Presenter.bpf.c',
    'Kaliber-MMOmentum.bpf.c',
    'Wacom-ArtPen.bpf.c',
    'XBox_Elite_2.bpf.c',
    'xppen-Artist24.bpf.c',
    'xppen-ArtistPro16Gen2.bpf.c',
]

foreach bpf : sources
    filter_matches = not have_filters
    if have_filters
        foreach filter: filters
            if bpf.contains(filter)
                filter_matches = true
                break
            endif
        endforeach
    endif
    if filter_matches
        bpf_sources += files(bpf)
        # This is a dummy target so meson thinks there's something here that
        # we can install. The actual files are generated by the cargo command
        # but adding a custom command here means we can install the files.
        bpf_o_files_testing += custom_target('fake-@0@.o'.format(bpf),
            input: bpf,
            depend_files: bpf,
            output: ['@BASENAME@.o'],
            command: ['true'],
            install: true,
            install_dir: bpfdir,
        )
    endif
endforeach
